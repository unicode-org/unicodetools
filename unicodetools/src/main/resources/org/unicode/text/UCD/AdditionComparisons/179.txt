# Latin (and Punctuation): English Phonotypic Alphabet
# https://github.com/unicode-org/utc-release-management/issues/179

# Names always differ.
# Age always differs since these tests are comparing additions to pre-existing characters.
Ignoring Name Age:

# Ignore the security and IDNA properties, as these are not yet included for provisionally assigned characters.
Ignoring Confusable_MA Identifier_Status Identifier_Type Idn_Status Idn_Mapping Idn_2008:

Ignoring Unicode_1_Name:
Propertywise [\x{A7DD}   \N{LATIN CAPITAL LETTER CLOSED OMEGA}]
           : [\x{0277} …∑ \N{LATIN SMALL LETTER CLOSED OMEGA}]
CorrespondTo [\x{A7AD} Íû≠ \N{LATIN CAPITAL LETTER L WITH BELT}]
           : [\x{026C} …¨ \N{LATIN SMALL LETTER L WITH BELT}]
end Ignoring;

Propertywise [\x{1DF68} \N{LATIN CAPITAL LETTER PHONOTYPIC A WITH SWASH}
              \x{1DF6A} \N{LATIN CAPITAL LETTER PHONOTYPIC ROUNDTOP A}
              \x{1DF6C} \N{LATIN CAPITAL LETTER REVERSED SCRUPLE}
              \x{1DF6E} \N{LATIN CAPITAL LETTER PHONOTYPIC DIPHTHONG AI}
              \x{1DF72} \N{LATIN CAPITAL LETTER O WITH CURL}
              \x{1DF74} \N{LATIN CAPITAL LETTER CLOSED OMEGA WITH LONG STEM}
              \x{1DF76} \N{LATIN CAPITAL LETTER TURNED CLOSED OMEGA}
              \x{1DF78} \N{LATIN CAPITAL LETTER PHONOTYPIC TH}
              \x{1DF7A} \N{LATIN CAPITAL LETTER U WITH HOOK TAIL}
              \x{1DF7C} \N{LATIN CAPITAL LETTER U WITH NOTCH AT BOTTOM}
              \x{1DF7E} \N{LATIN CAPITAL LETTER REVERSED ENLARGED SMALL U}]
           : [\x{1DF69} \N{LATIN SMALL LETTER PHONOTYPIC A WITH SWASH}
              \x{1DF6B} \N{LATIN SMALL LETTER PHONOTYPIC ROUNDTOP A}
              \x{1DF6D} \N{LATIN SMALL LETTER REVERSED SCRUPLE}
              \x{1DF6F} \N{LATIN SMALL LETTER PHONOTYPIC DIPHTHONG AI}
              \x{1DF73} \N{LATIN SMALL LETTER O WITH CURL}
              \x{1DF75} \N{LATIN SMALL LETTER CLOSED OMEGA WITH LONG STEM}
              \x{1DF77} \N{LATIN SMALL LETTER TURNED CLOSED OMEGA}
              \x{1DF79} \N{LATIN SMALL LETTER PHONOTYPIC TH}
              \x{1DF7B} \N{LATIN SMALL LETTER U WITH HOOK TAIL}
              \x{1DF7D} \N{LATIN SMALL LETTER U WITH NOTCH AT BOTTOM}
              \x{1DF7F} \N{LATIN SMALL LETTER REVERSED U}]
CorrespondTo [\x{A79A} Íûö \N{LATIN CAPITAL LETTER VOLAPUK AE}]
           : [\x{A79B} Íûõ \N{LATIN SMALL LETTER VOLAPUK AE}]
    UpTo: Block (Latin_Extended_G vs Latin_Extended_D)

Propertywise [
    \x{1DF70}   \N{LATIN SMALL LETTER I WITH PIGTAIL AT BOTTOM}
    \x{1DF71}   \N{LATIN SMALL LETTER STRETCHED I}
    \x{1DF03} ùºÉ \N{LATIN SMALL LETTER REVERSED K}
] AreAlike

Ignoring
    Unicode_1_Name Block
    Decomposition_Mapping Decomposition_Type
        FC_NFKC_Closure NFKC_Casefold NFKC_Simple_Casefold NFKC_Quick_Check NFKD_Quick_Check
        Changes_When_NFKC_Casefolded
    Script Script_Extensions
    Math Other_Math:
Propertywise [
    \x{1DF80}  \N{LATIN CAPITAL LETTER A WITH TOPBAR}
    \x{1DF81}  \N{LATIN CAPITAL LETTER E WITH BENT TOPBAR}
    \x{03D2} œí \N{GREEK UPSILON WITH HOOK SYMBOL}
] AreAlike
end Ignoring;

Ignoring General_Category Lowercase Uppercase Sentence_Break:
Propertywise [
    \x{1DF80}  \N{LATIN CAPITAL LETTER A WITH TOPBAR}
    \x{1DF81}  \N{LATIN CAPITAL LETTER E WITH BENT TOPBAR}
    \x{1DF03} ùºÉ \N{LATIN SMALL LETTER REVERSED K}
] AreAlike
end Ignoring;

end Ignoring;

end Ignoring;